# Maintainer: Joan Bruguera Mic√≥ <joanbrugueram@gmail.com>
pkgname=full-offline-backup-for-todoist-git
pkgver=0.1
pkgrel=1
pkgdesc="Small, dependency-less Python script to make a backup of all Todoist tasks and attachments that is accessible offline"
arch=('any')
url="https://github.com/joanbm/full-offline-backup-for-todoist"
license=('GPLv3')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
provides=('full-offline-backup-for-todoist')
conflicts=('full-offline-backup-for-todoist')
source=('git+https://github.com/joanbm/full-offline-backup-for-todoist.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python -m build --wheel --no-isolation
}

check() {
	cd "$srcdir/${pkgname%-git}"
	python -m unittest
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
